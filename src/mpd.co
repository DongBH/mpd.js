######################### private static #####################
const diacritics_removal_map = [
    {'base':'A', 'regex':/[\u0041\u24B6\uFF21\u00C0\u00C1\u00C2\u1EA6\u1EA4\u1EAA\u1EA8\u00C3\u0100\u0102\u1EB0\u1EAE\u1EB4\u1EB2\u0226\u01E0\u00C4\u01DE\u1EA2\u00C5\u01FA\u01CD\u0200\u0202\u1EA0\u1EAC\u1EB6\u1E00\u0104\u023A\u2C6F]/g},
    {'base':'AA','regex':/[\uA732]/g},
    {'base':'AE','regex':/[\u00C6\u01FC\u01E2]/g},
    {'base':'AO','regex':/[\uA734]/g},
    {'base':'AU','regex':/[\uA736]/g},
    {'base':'AV','regex':/[\uA738\uA73A]/g},
    {'base':'AY','regex':/[\uA73C]/g},
    {'base':'B', 'regex':/[\u0042\u24B7\uFF22\u1E02\u1E04\u1E06\u0243\u0182\u0181]/g},
    {'base':'C', 'regex':/[\u0043\u24B8\uFF23\u0106\u0108\u010A\u010C\u00C7\u1E08\u0187\u023B\uA73E]/g},
    {'base':'D', 'regex':/[\u0044\u24B9\uFF24\u1E0A\u010E\u1E0C\u1E10\u1E12\u1E0E\u0110\u018B\u018A\u0189\uA779]/g},
    {'base':'DZ','regex':/[\u01F1\u01C4]/g},
    {'base':'Dz','regex':/[\u01F2\u01C5]/g},
    {'base':'E', 'regex':/[\u0045\u24BA\uFF25\u00C8\u00C9\u00CA\u1EC0\u1EBE\u1EC4\u1EC2\u1EBC\u0112\u1E14\u1E16\u0114\u0116\u00CB\u1EBA\u011A\u0204\u0206\u1EB8\u1EC6\u0228\u1E1C\u0118\u1E18\u1E1A\u0190\u018E]/g},
    {'base':'F', 'regex':/[\u0046\u24BB\uFF26\u1E1E\u0191\uA77B]/g},
    {'base':'G', 'regex':/[\u0047\u24BC\uFF27\u01F4\u011C\u1E20\u011E\u0120\u01E6\u0122\u01E4\u0193\uA7A0\uA77D\uA77E]/g},
    {'base':'H', 'regex':/[\u0048\u24BD\uFF28\u0124\u1E22\u1E26\u021E\u1E24\u1E28\u1E2A\u0126\u2C67\u2C75\uA78D]/g},
    {'base':'I', 'regex':/[\u0049\u24BE\uFF29\u00CC\u00CD\u00CE\u0128\u012A\u012C\u0130\u00CF\u1E2E\u1EC8\u01CF\u0208\u020A\u1ECA\u012E\u1E2C\u0197]/g},
    {'base':'J', 'regex':/[\u004A\u24BF\uFF2A\u0134\u0248]/g},
    {'base':'K', 'regex':/[\u004B\u24C0\uFF2B\u1E30\u01E8\u1E32\u0136\u1E34\u0198\u2C69\uA740\uA742\uA744\uA7A2]/g},
    {'base':'L', 'regex':/[\u004C\u24C1\uFF2C\u013F\u0139\u013D\u1E36\u1E38\u013B\u1E3C\u1E3A\u0141\u023D\u2C62\u2C60\uA748\uA746\uA780]/g},
    {'base':'LJ','regex':/[\u01C7]/g},
    {'base':'Lj','regex':/[\u01C8]/g},
    {'base':'M', 'regex':/[\u004D\u24C2\uFF2D\u1E3E\u1E40\u1E42\u2C6E\u019C]/g},
    {'base':'N', 'regex':/[\u004E\u24C3\uFF2E\u01F8\u0143\u00D1\u1E44\u0147\u1E46\u0145\u1E4A\u1E48\u0220\u019D\uA790\uA7A4]/g},
    {'base':'NJ','regex':/[\u01CA]/g},
    {'base':'Nj','regex':/[\u01CB]/g},
    {'base':'O', 'regex':/[\u004F\u24C4\uFF2F\u00D2\u00D3\u00D4\u1ED2\u1ED0\u1ED6\u1ED4\u00D5\u1E4C\u022C\u1E4E\u014C\u1E50\u1E52\u014E\u022E\u0230\u00D6\u022A\u1ECE\u0150\u01D1\u020C\u020E\u01A0\u1EDC\u1EDA\u1EE0\u1EDE\u1EE2\u1ECC\u1ED8\u01EA\u01EC\u00D8\u01FE\u0186\u019F\uA74A\uA74C]/g},
    {'base':'OI','regex':/[\u01A2]/g},
    {'base':'OO','regex':/[\uA74E]/g},
    {'base':'OU','regex':/[\u0222]/g},
    {'base':'P', 'regex':/[\u0050\u24C5\uFF30\u1E54\u1E56\u01A4\u2C63\uA750\uA752\uA754]/g},
    {'base':'Q', 'regex':/[\u0051\u24C6\uFF31\uA756\uA758\u024A]/g},
    {'base':'R', 'regex':/[\u0052\u24C7\uFF32\u0154\u1E58\u0158\u0210\u0212\u1E5A\u1E5C\u0156\u1E5E\u024C\u2C64\uA75A\uA7A6\uA782]/g},
    {'base':'S', 'regex':/[\u0053\u24C8\uFF33\u1E9E\u015A\u1E64\u015C\u1E60\u0160\u1E66\u1E62\u1E68\u0218\u015E\u2C7E\uA7A8\uA784]/g},
    {'base':'T', 'regex':/[\u0054\u24C9\uFF34\u1E6A\u0164\u1E6C\u021A\u0162\u1E70\u1E6E\u0166\u01AC\u01AE\u023E\uA786]/g},
    {'base':'TZ','regex':/[\uA728]/g},
    {'base':'U', 'regex':/[\u0055\u24CA\uFF35\u00D9\u00DA\u00DB\u0168\u1E78\u016A\u1E7A\u016C\u00DC\u01DB\u01D7\u01D5\u01D9\u1EE6\u016E\u0170\u01D3\u0214\u0216\u01AF\u1EEA\u1EE8\u1EEE\u1EEC\u1EF0\u1EE4\u1E72\u0172\u1E76\u1E74\u0244]/g},
    {'base':'V', 'regex':/[\u0056\u24CB\uFF36\u1E7C\u1E7E\u01B2\uA75E\u0245]/g},
    {'base':'VY','regex':/[\uA760]/g},
    {'base':'W', 'regex':/[\u0057\u24CC\uFF37\u1E80\u1E82\u0174\u1E86\u1E84\u1E88\u2C72]/g},
    {'base':'X', 'regex':/[\u0058\u24CD\uFF38\u1E8A\u1E8C]/g},
    {'base':'Y', 'regex':/[\u0059\u24CE\uFF39\u1EF2\u00DD\u0176\u1EF8\u0232\u1E8E\u0178\u1EF6\u1EF4\u01B3\u024E\u1EFE]/g},
    {'base':'Z', 'regex':/[\u005A\u24CF\uFF3A\u0179\u1E90\u017B\u017D\u1E92\u1E94\u01B5\u0224\u2C7F\u2C6B\uA762]/g},
    {'base':'a', 'regex':/[\u0061\u24D0\uFF41\u1E9A\u00E0\u00E1\u00E2\u1EA7\u1EA5\u1EAB\u1EA9\u00E3\u0101\u0103\u1EB1\u1EAF\u1EB5\u1EB3\u0227\u01E1\u00E4\u01DF\u1EA3\u00E5\u01FB\u01CE\u0201\u0203\u1EA1\u1EAD\u1EB7\u1E01\u0105\u2C65\u0250]/g},
    {'base':'aa','regex':/[\uA733]/g},
    {'base':'ae','regex':/[\u00E6\u01FD\u01E3]/g},
    {'base':'ao','regex':/[\uA735]/g},
    {'base':'au','regex':/[\uA737]/g},
    {'base':'av','regex':/[\uA739\uA73B]/g},
    {'base':'ay','regex':/[\uA73D]/g},
    {'base':'b', 'regex':/[\u0062\u24D1\uFF42\u1E03\u1E05\u1E07\u0180\u0183\u0253]/g},
    {'base':'c', 'regex':/[\u0063\u24D2\uFF43\u0107\u0109\u010B\u010D\u00E7\u1E09\u0188\u023C\uA73F\u2184]/g},
    {'base':'d', 'regex':/[\u0064\u24D3\uFF44\u1E0B\u010F\u1E0D\u1E11\u1E13\u1E0F\u0111\u018C\u0256\u0257\uA77A]/g},
    {'base':'dz','regex':/[\u01F3\u01C6]/g},
    {'base':'e', 'regex':/[\u0065\u24D4\uFF45\u00E8\u00E9\u00EA\u1EC1\u1EBF\u1EC5\u1EC3\u1EBD\u0113\u1E15\u1E17\u0115\u0117\u00EB\u1EBB\u011B\u0205\u0207\u1EB9\u1EC7\u0229\u1E1D\u0119\u1E19\u1E1B\u0247\u025B\u01DD]/g},
    {'base':'f', 'regex':/[\u0066\u24D5\uFF46\u1E1F\u0192\uA77C]/g},
    {'base':'g', 'regex':/[\u0067\u24D6\uFF47\u01F5\u011D\u1E21\u011F\u0121\u01E7\u0123\u01E5\u0260\uA7A1\u1D79\uA77F]/g},
    {'base':'h', 'regex':/[\u0068\u24D7\uFF48\u0125\u1E23\u1E27\u021F\u1E25\u1E29\u1E2B\u1E96\u0127\u2C68\u2C76\u0265]/g},
    {'base':'hv','regex':/[\u0195]/g},
    {'base':'i', 'regex':/[\u0069\u24D8\uFF49\u00EC\u00ED\u00EE\u0129\u012B\u012D\u00EF\u1E2F\u1EC9\u01D0\u0209\u020B\u1ECB\u012F\u1E2D\u0268\u0131]/g},
    {'base':'j', 'regex':/[\u006A\u24D9\uFF4A\u0135\u01F0\u0249]/g},
    {'base':'k', 'regex':/[\u006B\u24DA\uFF4B\u1E31\u01E9\u1E33\u0137\u1E35\u0199\u2C6A\uA741\uA743\uA745\uA7A3]/g},
    {'base':'l', 'regex':/[\u006C\u24DB\uFF4C\u0140\u013A\u013E\u1E37\u1E39\u013C\u1E3D\u1E3B\u017F\u0142\u019A\u026B\u2C61\uA749\uA781\uA747]/g},
    {'base':'lj','regex':/[\u01C9]/g},
    {'base':'m', 'regex':/[\u006D\u24DC\uFF4D\u1E3F\u1E41\u1E43\u0271\u026F]/g},
    {'base':'n', 'regex':/[\u006E\u24DD\uFF4E\u01F9\u0144\u00F1\u1E45\u0148\u1E47\u0146\u1E4B\u1E49\u019E\u0272\u0149\uA791\uA7A5]/g},
    {'base':'nj','regex':/[\u01CC]/g},
    {'base':'o', 'regex':/[\u006F\u24DE\uFF4F\u00F2\u00F3\u00F4\u1ED3\u1ED1\u1ED7\u1ED5\u00F5\u1E4D\u022D\u1E4F\u014D\u1E51\u1E53\u014F\u022F\u0231\u00F6\u022B\u1ECF\u0151\u01D2\u020D\u020F\u01A1\u1EDD\u1EDB\u1EE1\u1EDF\u1EE3\u1ECD\u1ED9\u01EB\u01ED\u00F8\u01FF\u0254\uA74B\uA74D\u0275]/g},
    {'base':'oi','regex':/[\u01A3]/g},
    {'base':'ou','regex':/[\u0223]/g},
    {'base':'oo','regex':/[\uA74F]/g},
    {'base':'p','regex':/[\u0070\u24DF\uFF50\u1E55\u1E57\u01A5\u1D7D\uA751\uA753\uA755]/g},
    {'base':'q','regex':/[\u0071\u24E0\uFF51\u024B\uA757\uA759]/g},
    {'base':'r','regex':/[\u0072\u24E1\uFF52\u0155\u1E59\u0159\u0211\u0213\u1E5B\u1E5D\u0157\u1E5F\u024D\u027D\uA75B\uA7A7\uA783]/g},
    {'base':'s','regex':/[\u0073\u24E2\uFF53\u00DF\u015B\u1E65\u015D\u1E61\u0161\u1E67\u1E63\u1E69\u0219\u015F\u023F\uA7A9\uA785\u1E9B]/g},
    {'base':'t','regex':/[\u0074\u24E3\uFF54\u1E6B\u1E97\u0165\u1E6D\u021B\u0163\u1E71\u1E6F\u0167\u01AD\u0288\u2C66\uA787]/g},
    {'base':'tz','regex':/[\uA729]/g},
    {'base':'u','regex':/[\u0075\u24E4\uFF55\u00F9\u00FA\u00FB\u0169\u1E79\u016B\u1E7B\u016D\u00FC\u01DC\u01D8\u01D6\u01DA\u1EE7\u016F\u0171\u01D4\u0215\u0217\u01B0\u1EEB\u1EE9\u1EEF\u1EED\u1EF1\u1EE5\u1E73\u0173\u1E77\u1E75\u0289]/g},
    {'base':'v','regex':/[\u0076\u24E5\uFF56\u1E7D\u1E7F\u028B\uA75F\u028C]/g},
    {'base':'vy','regex':/[\uA761]/g},
    {'base':'w','regex':/[\u0077\u24E6\uFF57\u1E81\u1E83\u0175\u1E87\u1E85\u1E98\u1E89\u2C73]/g},
    {'base':'x','regex':/[\u0078\u24E7\uFF58\u1E8B\u1E8D]/g},
    {'base':'y','regex':/[\u0079\u24E8\uFF59\u1EF3\u00FD\u0177\u1EF9\u0233\u1E8F\u00FF\u1EF7\u1E99\u1EF5\u01B4\u024F\u1EFF]/g},
    {'base':'z','regex':/[\u007A\u24E9\uFF5A\u017A\u1E91\u017C\u017E\u1E93\u1E95\u01B6\u0225\u0240\u2C6C\uA763]/g}
]
const removeDiacritics = (str) ->
  str .= replace(regex, base) for {regex, base} of diacritics_removal_map
  str

const strip_prefixes = [/^the\s+/, /^a\s+/, /^an\s+/]
const stripPrefixes = (str) ->
  for regex of strip_prefixes
    str .= replace(regex, '')
    break
  str

const DEFAULT_ARTIST = "[Unknown Artist]"
const VARIOUS_ARTISTS = "Various Artists"

const MPD_SENTINEL = /^(OK|ACK|list_OK)(.*)$/m

const trim = do
  __trim = String.prototype.trim
  if __trim?
    (text) -> if text? then __trim.call(text) else ""
  else
    __trimLeft = /^\s+/
    __trimRight = /\s+$/
    (text) ->
      if text? then text.toString().replace(__trimLeft, "").replace(__trimRight, "") else ""

const elapsedToDate = (elapsed) -> new Date((new Date()) - elapsed * 1000)
const dateToElapsed = (date) -> ((new Date()) - date) / 1000

const fromMpdVol = (vol) ->
  vol = parseInt(vol)
  if vol < 0 or vol > 100 then null else vol / 100

const toMpdVol = (vol) -> 0 >? Math.round(parseFloat(vol) * 100) <? 100

const sortableTitle = (title) -> stripPrefixes(removeDiacritics(title).toLowerCase())

const titleCompare = (a, b) ->
  _a = sortableTitle(a)
  _b = sortableTitle(b)
  if _a < _b
    -1
  else if _a > _b
    1
  else
    # At this point we compare the original strings. Our cache update code
    # depends on this behavior.
    if a < b
      -1
    else if a > b
      1
    else
      0

const noop = ->

const qEscape = (str) ->
  # replace all " with \"
  str.toString().replace /"/g, '\\"'

const pickNRandomProps = (obj, n) ->
  return [] if n is 0
  results = []
  count = 0
  for prop in obj
    count += 1
    for i from 0 til n
      if Math.random() < 1 / count
        results[i] = prop
  results

const sign = (n) -> if n > 0 then 1 else if n < 0 then -1 else 0

const boolToInt = (b) -> if b then 1 else 0

const parseMaybeUndefNumber = (n) ->
  n = parseInt(n)
  n = "" if isNaN(n)
  n

const split_once = (line, separator) ->
  # should be line.split(separator, 1), but javascript is stupid
  index = line.indexOf(separator)
  [line.substr(0, index), line.substr(index + separator.length)]

class EventEmitter
  ..count = 0

  ~>
    @event_handlers = {}
    @next_id = 0
    @prop = "__EventEmitter_#{..count++}_id"

  on: !(event_name, handler) ->
    handler[@prop] = @next_id
    @event_handlers@[event_name][@next_id] = handler
    @next_id += 1

  removeListener: !(event_name, handler) ->
    delete @event_handlers@[event_name][handler[@prop]]

  # protected
  emit: !(event_name, ...args) ->
    for id, h in @event_handlers@[event_name]
      h(...args)


class Mpd extends EventEmitter
  ######################### static ######################
  ..trackNameFromFile = (filename) ->
    filetitle = filename.substr(filename.lastIndexOf('/') + 1)
    len = if (dot = filetitle.lastIndexOf('.')) >= 0 then dot else filetitle.length
    filetitle.substr 0, len

  ######################### public #####################
  ~>
    super()
    @resetServerState()

    @event_handlers = {}

    # maps mpd subsystems to our function to call which will update ourself
    @updateFuncs =
      database: ~> # the song database has been modified after update.
        @have_file_list_cache = false
        @updateLibrary()
      update: noop # a database update has started or finished. If the database was modified during the update, the database event is also emitted.
      stored_playlist: noop # a stored playlist has been modified, renamed, created or deleted
      playlist: @~updatePlaylist # the current playlist has been modified
      player: @~updateStatus # the player has been started, stopped or seeked
      mixer: @~updateStatus # the volume has been changed
      output: noop # an audio output has been enabled or disabled
      options: @~updateStatus # options like repeat, random, crossfade, replay gain
      sticker: ~> # the sticker database has been modified.
        @emit \stickerupdate
      subscription: noop # a client has subscribed or unsubscribed to a channel
      message: noop # a message was received on a channel this client is subscribed to; this event is only emitted when the queue is empty

  artistKey: (artist_name) ->
    if artist_name is DEFAULT_ARTIST then "" else artist_name.toLowerCase()

  getArtistAlbums: (artist_name) ->
    @search_results.artist_table[@artistKey(artist_name)].albums

  handleConnectionStart: !->
    @sendCommand "subscribe Status"
    @updateLibrary()
    @updateStatus()
    @updatePlaylist()

  sendCommand: !(command, callback=noop) ->
    @send "noidle\n" if @idling
    @sendWithCallback command, callback
    @sendWithCallback "idle", @~handleIdleResultsLoop
    @idling = true # we're always idling after the first command.

  sendCommands: !(command_list, callback=noop) ->
    return if command_list.length is 0
    @sendCommand "command_list_begin\n#{command_list.join("\n")}\ncommand_list_end", callback

  updateLibrary: !->
    @sendCommand 'listallinfo', !(err, msg) ~>
      if err then throw err
      tracks = @mpdTracksToTrackObjects @parseMpdTracks msg
      @buildArtistAlbumTree tracks, @library
      @have_file_list_cache = true
      # in case the user has a search open, we'll apply their search again.
      last_query = @last_query
      # reset last query so that search is forced to run again
      @last_query = ""
      @search last_query

  updatePlaylist: !(callback=noop) ->
    @sendCommand "playlistinfo", !(err, msg) ~>
      return callback(err) if err
      mpd_tracks = @parseMpdTracks msg
      @clearPlaylist()
      for mpd_track of mpd_tracks
        id = parseInt(mpd_track.Id)
        obj =
          id: id
          track: @library.track_table[mpd_track.file]
          pos: @playlist.item_list.length
        @playlist.item_list.push obj
        @playlist.item_table[id] = obj

      # make sure current track data is correct
      if @status.current_item?
        @status.current_item = @playlist.item_table[@status.current_item.id]

      if @status.current_item?
        # looks good, notify listeners
        @emit 'playlistupdate'
        callback()
      else
        # we need a status update before raising a playlist update event
        @updateStatus !(err) ~>
          return callback(err) if err
          @emit 'playlistupdate'
          callback()

  updateStatus: !(callback=noop) ->
    @sendCommand "status", !(err, msg) ~>
      throw err if err
      # no dict comprehensions :(
      # https://github.com/satyr/coco/issues/102
      o = {}
      for [key, val] of (split_once(line, ": ") for line of msg.split("\n"))
        o[key] = val
      @status <<<
        volume: fromMpdVol(o.volume)
        repeat: parseInt(o.repeat) is not 0
        random: parseInt(o.random) is not 0
        single: parseInt(o.single) is not 0
        consume: parseInt(o.consume) is not 0
        state: o.state
        time: null
        bitrate: null
        track_start_date: null

      @status.bitrate = parseInt(o.bitrate) if o.bitrate?

      if o.time? and o.elapsed?
        @status.time = parseInt(o.time.split(":")[1])
        # we still add elapsed for when its paused
        @status.elapsed = parseFloat(o.elapsed)
        # add a field for the start date of the current track
        @status.track_start_date = elapsedToDate(@status.elapsed)

    @sendCommand "currentsong", !(err, msg) ~>
      return callback(err) if err

      mpd_tracks = @parseMpdTracks msg
      if mpd_tracks.length is 0
        # no current song
        @status.current_item = null
        callback()
        @emit 'statusupdate'
        return

      throw "error" if mpd_tracks.length not of [0, 1]
      for mpd_track of mpd_tracks
        id = parseInt(mpd_track.Id)
        pos = parseInt(mpd_track.Pos)

        @status.current_item = @playlist.item_table[id]

        if @status.current_item? and @status.current_item.pos is pos
          @status.current_item.track = @library.track_table[mpd_track.file]
          # looks good, notify listeners
          @emit 'statusupdate'
          callback()
        else
          # missing or inconsistent playlist data, need to get playlist update
          @status.current_item =
            id: id
            pos: pos
            track: @library.track_table[mpd_track.file]
          @updatePlaylist !(err) ~>
            return callback(err) if err
            @emit 'statusupdate'
            callback()

  # puts the search results in search_results
  search: !(query) ->
    query = trim(query)
    if query.length is 0
      @search_results = @library
      @emit \libraryupdate
      @last_query = query
      return
    words = removeDiacritics(query).toLowerCase().split(/\s+/)
    query = words.join(" ")
    return if query is @last_query
    @last_query = query
    result = []
    for k, track in @library.track_table
      is_match = do ->
        for word of words
          if track.search_tags.indexOf(word) is -1
            return false
        true
      result.push track if is_match
    # zip results into album
    @buildArtistAlbumTree result, @search_results = {}
    @emit \libraryupdate

  queueRandomTracks: !(n) ->
    @sendCommands @queueRandomTracksCommands n

  queueFiles: !(files, pos=@playlist.item_list.length, callback=noop) ->
    return callback(null, []) unless files.length
    cmds = []
    for file of files by -1
      cmds.push "addid \"#{qEscape(file)}\" #{pos}"

    items = ({id: null, pos: null, track: @library.track_table[file]} for file of files)
    @playlist.item_list.splice pos, 0, ...items
    @fixPlaylistPosCache()

    @sendCommands cmds, !(err, msg) ~>
      return callback(err) if err
      for line, i of msg.split("\n")
        continue unless line
        index = files.length - 1 - i
        item_id = parseInt(line.substring(4))
        items[index].id = item_id
      callback(null, items)

    @emit 'playlistupdate'

  queueFile: !(file, pos) -> @queueFiles [file], pos

  queueFilesNext: !(files) ->
    new_pos = (@status.current_item?.pos ? -1) + 1
    @queueFiles files, new_pos

  queueFileNext: !(file) -> @queueFilesNext [file]

  clear: !->
    @sendCommand "clear"
    @clearPlaylist()
    @emit 'playlistupdate'

  shuffle: !-> @sendCommand "shuffle"

  stop: !->
    @sendCommand "stop"
    @status.state = "stop"
    @emit 'statusupdate'

  play: !->
    @sendCommand "play"

    if @status.state is "pause"
      @status.track_start_date = elapsedToDate(@status.elapsed)
      @status.state = "play"
      @emit 'statusupdate'

  pause: !->
    @sendCommand "pause 1"

    if @status.state is "play"
      @status.elapsed = dateToElapsed(@status.track_start_date)
      @status.state = "pause"
      @emit 'statusupdate'

  next: !->
    @sendCommand "next"
    @anticipateSkip 1

  prev: !->
    @sendCommand "previous"
    @anticipateSkip -1

  playId: !(track_id) ->
    track_id = parseInt(track_id)
    @sendCommand "playid #{qEscape(track_id)}"
    @anticipatePlayId track_id

  moveIds: !(track_ids, pos) ->
    pos = parseInt(pos)
    # get the playlist items for the ids
    items = (item if (item = @playlist.item_table[id])? for id of track_ids)
    # sort the list by the reverse order in the playlist
    items.sort (a, b) -> b.pos - a.pos

    cmds = []
    while items.length > 0
      if pos <= items[0].pos
        real_pos = pos
        item = items.shift()
      else
        real_pos = pos - 1
        item = items.pop()

      cmds.push "moveid #{item.id} #{real_pos}"
      @playlist.item_list.splice item.pos, 1
      @playlist.item_list.splice real_pos, 0, item

      @fixPlaylistPosCache()

    @sendCommands cmds
    @emit 'playlistupdate'

  # shifts the list of ids by offset, winamp style
  shiftIds: !(track_ids, offset) ->
    offset = parseInt(offset)
    return if offset is 0 or track_ids.length is 0

    items = (item if (item = @playlist.item_table[id])? for id of track_ids)
    items.sort (a, b) -> sign(offset) * (b.pos - a.pos)

    # abort if any are out of bounds
    for item of [items[0], items[items.length - 1]]
      new_pos = item.pos + offset
      return if new_pos < 0 or new_pos >= @playlist.item_list.length

    @sendCommands ("moveid #{item.id} #{item.pos + offset}" for item of items)

    # anticipate the result
    for item of items
      @playlist.item_list.splice item.pos, 1
      @playlist.item_list.splice item.pos+offset, 0, item
      @fixPlaylistPosCache()

    @emit 'playlistupdate'

  removeIds: !(track_ids, callback=noop) ->
    return callback() if track_ids.length is 0
    cmds = []
    for track_id of track_ids
      track_id = parseInt(track_id)
      if @status.current_item?.id is track_id
        @anticipateSkip 1
        if @status.state is not "play"
          @status.state = "stop"
      cmds.push "deleteid #{qEscape(track_id)}"
      item = @playlist.item_table[track_id]
      delete @playlist.item_table[item.id]
      @playlist.item_list.splice(item.pos, 1)
      @fixPlaylistPosCache()

    @sendCommands cmds, (err) ~> callback(err)
    @emit 'playlistupdate'

  removeId: !(track_id) ->
    @removeIds [track_id]

  close: !-> @send "close\n" # bypass message queue

  # in seconds
  seek: !(pos) ->
    pos = parseFloat(pos)
    pos = 0 if pos < 0
    pos = @status.time if pos > @status.time
    @sendCommand "seekid #{@status.current_item.id} #{Math.round(pos)}"
    @status.track_start_date = elapsedToDate(pos)
    @emit 'statusupdate'

  # between 0 and 1
  setVolume: !(vol) ->
    vol = toMpdVol(vol)
    @sendCommand "setvol #{vol}"
    @status.volume = fromMpdVol(vol)
    @emit 'statusupdate'

  changeStatus: !(status) ->
    cmds = []
    if status.consume?
      @status.consume = status.consume
      cmds.push "consume #{boolToInt(status.consume)}"
    if status.random?
      @status.random = status.random
      cmds.push "random #{boolToInt(status.random)}"
    if status.repeat?
      @status.repeat = status.repeat
      cmds.push "repeat #{boolToInt(status.repeat)}"
    if status.single?
      @status.single = status.single
      cmds.push "single #{boolToInt(status.single)}"

    @sendCommands cmds
    @emit 'statusupdate'

  getFileInfo: !(file, cb=noop) ->
    @sendCommand "lsinfo \"#{qEscape(file)}\"", !(err, msg) ~>
      return cb(err) if err
      [track] = @mpdTracksToTrackObjects @parseMpdTracks msg
      cb null, track

  authenticate: !(password, callback=noop) ->
    @sendCommand "password \"#{qEscape(password)}\"", !(err) ~> callback(err)

  scanFiles: !(files) ->
    @sendCommands ("update \"#{qEscape(file)}\"" for file of files)

  findStickers: !(dir, name, cb=noop) ->
    @sendCommand "sticker find song \"#{qEscape(dir)}\" \"#{qEscape(name)}\"", !(err, msg) ~>
      return cb(err) if err
      current_file = null
      stickers = {}
      for line of msg.split("\n")
        [name, value] = split_once line, ": "
        if name is "file"
          current_file = value
        else if name is "sticker"
          return cb("protocol") if current_file!?
          value = split_once(value, "=")[1]
          stickers[current_file] = value
      cb(null, stickers)

  setStickers: !(files, name, value, cb=noop) ->
    cmds = for file of files
      "sticker set song \"#{qEscape(file)}\" \"#{qEscape(name)}\" \"#{qEscape(value)}\""
    @sendCommands cmds, (err) ~> cb(err)

  setSticker: !(file, name, value, cb=noop) ->
    @setStickers [file], name, value, cb

  ######################### private #####################

  handleMessage: !(err, msg) ->
    handler = @msg_handler_queue.shift()
    handler(err, msg)

  receive: !(data) ->
    @buffer += data

    for ever
      m = @buffer.match(MPD_SENTINEL)
      return if not m?

      msg = @buffer.substring(0, m.index)
      [line, code, str] = m
      if code is "ACK"
        @emit 'error', str
        @handleMessage str
      else if line.indexOf("OK MPD") is 0
        # new connection
        @emit 'connect'
      else
        @handleMessage null, msg
      @buffer = @buffer.substring(msg.length+line.length+1)

  handleIdleResults: !(msg) ->
    for system of trim(msg).split("\n")
      if system.length > 0
        updateFunc = @updateFuncs[system.substring(9)] ? noop
        updateFunc()

  # cache of playlist data from mpd
  clearPlaylist: !->
    @playlist = {}
    @playlist.item_list = []
    @playlist.item_table = {}

  anticipatePlayId: !(track_id) ->
    item = @playlist.item_table[track_id]
    @status.current_item = item
    @status.state = "play"
    @status.time = item.track.time
    @status.track_start_date = new Date()
    @emit 'statusupdate'

  anticipateSkip: !(direction) ->
    next_item = @playlist.item_list[@status.current_item.pos + direction]
    if next_item?
      @anticipatePlayId next_item.id

  parseMpdTracks: (msg) ->
    if msg is ""
      return []

    # build list of tracks from msg
    mpd_tracks = []
    current_track = null
    flush_current_track = !->
      if current_track?
        mpd_tracks.push(current_track)
      current_track := {}
    for line of msg.split("\n")
      [key, value] = split_once line, ": "
      continue if key is 'directory'
      flush_current_track() if key is 'file'
      current_track[key] = value
    flush_current_track()
    mpd_tracks

  mpdTracksToTrackObjects: (mpd_tracks) ->
    tracks = []
    for mpd_track of mpd_tracks
      artist_name = trim(mpd_track.Artist)
      track =
        file: mpd_track.file
        name: mpd_track.Title || ..trackNameFromFile(mpd_track.file)
        artist_name: artist_name
        artist_disambiguation: ""
        album_artist_name: mpd_track.AlbumArtist or artist_name
        album_name: trim(mpd_track.Album)
        track: parseMaybeUndefNumber(mpd_track.Track)
        time: parseInt(mpd_track.Time)
        year: parseMaybeUndefNumber(mpd_track.Date)
      track.search_tags = removeDiacritics([track.artist_name, track.album_artist_name, track.album_name, track.name, track.file].join("\n")).toLowerCase()
      tracks.push track
    tracks

  getOrCreate = (key, table, initObjFunc) ->
    result = table[key]
    if result!?
      result = initObjFunc()
      # insert into table
      table[key] = result
    result
  makeComparator = (order_keys) -> (a, b) ->
    for order_key of order_keys
      a = a[order_key]
      b = b[order_key]
      if a < b
        return -1
      if a > b
        return 1
    return 0
  trackComparator = makeComparator ["track", "name"]
  albumComparator = makeComparator ["year", "name"]
  artistComparator = (a, b) -> titleCompare a.name, b.name
  buildArtistAlbumTree: !(tracks, library) ->
    # determine set of albums
    library.track_table = {}
    library.album_table = {}
    for track of tracks
      library.track_table[track.file] = track
      if track.album_name is ""
        album_key = track.album_artist_name + "\n"
      else
        album_key = track.album_name + "\n"
      album_key = album_key.toLowerCase()
      album = getOrCreate album_key, library.album_table, ->
        name: track.album_name
        year: track.year
        tracks: []
        key: album_key
      track.album = album
      album.tracks.push track
      album.year = album_year if not album.year?

    # find compilation albums and create artist objects
    artist_table = {}
    for k, album in library.album_table
      # count up all the artists and album artists mentioned in this album
      album_artists = {}
      album.tracks.sort trackComparator
      for track, i of album.tracks
        # cache the track indexes
        track.pos = i
        album_artist_name = track.album_artist_name
        album_artists[album_artist_name.toLowerCase()] = 1
        album_artists[track.artist_name.toLowerCase()] = 1
      artist_count = 0
      for k in album_artists
        artist_count += 1
      if artist_count > 1
        # multiple artists. we're sure it's a compilation album.
        album_artist_name = VARIOUS_ARTISTS
      if album_artist_name is VARIOUS_ARTISTS
        # make sure to disambiguate the artist names
        for track of album.tracks
          track.artist_disambiguation = track.artist_name
      artist = getOrCreate album_artist_name.toLowerCase(), artist_table, ->
        name: album_artist_name
        albums: []
      album.artist = artist
      artist.albums.push album

    # collect list of artists and sort albums
    library.artists = []
    various_artist = null
    for k, artist in artist_table
      artist.albums.sort albumComparator
      # cache the album indexes
      album.pos = i for album, i of artist.albums
      if artist.name is VARIOUS_ARTISTS
        various_artist = artist
      else
        library.artists.push artist

    # sort artists
    library.artists.sort artistComparator
    # various artists goes first
    library.artists.splice 0, 0, various_artist if various_artist?

    # cache the artist indexes
    artist.pos = i for artist, i of library.artists

    library.artist_table = artist_table

  sendWithCallback: !(cmd, cb=noop) ->
    @msg_handler_queue.push cb
    @send "#cmd\n"

  handleIdleResultsLoop: !(err, msg) ->
    if err then throw err
    @handleIdleResults(msg)
    # if we have nothing else to do, idle.
    if @msg_handler_queue.length is 0
      @sendWithCallback "idle", @~handleIdleResultsLoop

  fixPlaylistPosCache: !-> item.pos = i for item, i of @playlist.item_list

  # clear state so we can start over with new mpd connection
  resetServerState: !->
    @buffer = ""
    @msg_handler_queue = []

    # whether we've sent the idle command to mpd
    @idling = false

    @have_file_list_cache = false

    # cache of library data from mpd. See comment at top of this file
    @library =
      artists: []
      track_table: {}
    @search_results = @library
    @last_query = ""
    @clearPlaylist()
    @status =
      current_item: null

  queueRandomTracksCommands: (n) ->
    if not @have_file_list_cache
      return []
    ("addid \"#{qEscape(file)}\"" for file of pickNRandomProps(@library.track_table, n))

module?.exports = Mpd
window?.Mpd = Mpd
